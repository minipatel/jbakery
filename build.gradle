plugins {
    id 'org.ajoberstar.grgit' version '1.4.2'
    id 'org.ajoberstar.release-opinion' version '1.4.2'
    id 'net.gvmtool.sdkvendor' version '0.4.1'
}

group 'com.netdava.jbakery'

description = 'Jbakery :: aggregator project'


import org.ajoberstar.gradle.git.release.opinion.Strategies

release {
    grgit = org.ajoberstar.grgit.Grgit.open()
    defaultVersionStrategy = Strategies.SNAPSHOT
}

tasks.release.dependsOn ':distribution:bintrayUpload'

gvm {
    def releasedVersion = project.version.toString()
    api = "https://vendors.sdkman.io/"
    consumerKey = project.property("jbakery.consumerKey")
    consumerToken = project.property("jbakery.consumerToken")
    candidate = "jbakery"
    version = releasedVersion
    url = "https://bintray.com/artifact/download/netdava/maven/com/netdava/jbakery/jbakery-bin/${releasedVersion}/jbakery-bin-${releasedVersion}.tgz"
    hashtag = "#jbakery"
}

ext {
    def grgit = org.ajoberstar.grgit.Grgit.open()

    revision = grgit.head().id
    shortMessage = grgit.head().shortMessage

    // add these properties to your ~/.gradle/gradle.properties file
    bintrayUser = project.hasProperty('bintrayUser') ? bintrayUser : 'jenkins'
    bintrayApiKey = project.hasProperty('bintrayApiKey') ? bintrayApiKey : System.getenv("NEXUS_PASS")
}

allprojects {
    ext {
        vertxVersion = '3.2.1'
        springVersion = '4.2.5.RELEASE'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "http://dl.bintray.com/netdava/maven" }
    }
}
