plugins {
    id "com.moowork.node" version "0.11"
}

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'application'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    provided
    compile.extendsFrom provided
}

ext {
    vertxVersion = '3.2.1'
    springVersion = '4.2.5.RELEASE'
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.16.6'
    testCompileOnly 'org.projectlombok:lombok:1.16.6'

    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'org.apache.logging.log4j:log4j-api:2.4.1'
    compile 'org.apache.logging.log4j:log4j-core:2.4.1'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.4.1'

    compile 'org.eclipse.jgit:org.eclipse.jgit:4.1.0.201509280440-r'
    compile 'org.eclipse.jgit:org.eclipse.jgit.archive:4.1.0.201509280440-r'

    compile 'org.jbake:jbake-core:2.4.0'

    compile "org.springframework:spring-core:${springVersion}"
    compile "org.springframework:spring-context:${springVersion}"
    compile "org.springframework:spring-beans:${springVersion}"

    compile 'com.github.rworsnop:vertx-beans:1.2.1'

    compile "io.vertx:vertx-core:${vertxVersion}"
    compile "io.vertx:vertx-web:${vertxVersion}"

    compile "io.vertx:vertx-web-templ-thymeleaf:${vertxVersion}"
    compile "nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:1.3.1"

    compile "io.vertx:vertx-web-templ-handlebars:${vertxVersion}"
    compile "io.vertx:vertx-auth-shiro:${vertxVersion}"

    testCompile "io.vertx:vertx-unit:${vertxVersion}"
    testCompile "junit:junit:4.11"
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

mainClassName = "netdava.jbakery.JBakery"

// gradle + node + bower integration
// https://objectpartners.com/2015/04/29/using-gradle-and-bower-to-manage-jscss-dependencies/
node {
    version = '4.2.2'
    npmVersion = '2.14.4'
    download = true
}

task npmCacheConfig(type: NpmTask) {
    description = "Configure the NPM cache"
    def npmCacheDir = "${gradle.getGradleUserHomeDir()}/caches/npm"
    outputs.files file(npmCacheDir)
    args = ['config', 'set', 'cache', npmCacheDir]
}

task npmPackages(type: NpmTask, dependsOn: npmCacheConfig) {
    description = "Install Node.js packages"
    args = ['install']
    inputs.files file('package.json')
    outputs.files file('node_modules')
}

task bowerInstall(type: NodeTask) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('webroot/components')
    dependsOn npmPackages
}

build.dependsOn bowerInstall